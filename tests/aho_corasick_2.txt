class Node
{
    p;
    pchar;
    exit    = -1;
    link    = -1;
    leaf    = [];
    next    = [-1] * 26;
    go      = [-1] * 26;
    
    constructor (p = -1, pchar = -1)
    {
        this.p      = p;
        this.pchar  = pchar;
    }
}

func order (s, i)
{
    return code_of_char(s, i) - code_of_char("a", 0);
}

class AhoCorasick
{
    t = [new Node()];
    
    link (cur)
    {
        println("link ", cur);
        var t = this.t;
        if (t[cur].link === -1)
        {
            if (cur === 0 or t[cur].p === 0)
            {
                t[cur].link = 0;
            }
            else
            {
                t[cur].link = this.go(this.link(t[cur].p), t[cur].pchar);
            }
        }
        println("link ", t[cur].link);
        return t[cur].link;
    }
    
    go (cur, c)
    {
        println("go ", cur, " ", c);
        var t = this.t;
        if (t[cur].go[c] === -1)
        {
            if (t[cur].next[c] === -1)
            {
                if (cur === 0)
                {
                    t[cur].go[c] = 0;
                }
                else
                {
                    t[cur].go[c] = this.go(this.link(cur), c);
                }
            }
            else
            {
                t[cur].go[c] = t[cur].next[c];
            }
        }
        println("go ", t[cur].go[c]);
        return t[cur].go[c];
    }
    
    exit (cur)
    {
        var t = this.t;
        if (t[cur].exit === -1)
        {
            if (cur === 0 or this.link(cur) === 0)
            {
                t[cur].exit = 0;
            }
            else
            {
                if (len(t[this.link(cur)].leaf))
                {
                    t[cur].exit = this.link(cur);
                }
                else
                {
                    t[cur].exit = this.exit(this.link(cur));
                }
            }
        }
        
        return t[cur].exit;
    }
    
    add (word, index)
    {
        var cur = 0, t = this.t;
        
        for (var i = 0; i < len(word); i += 1)
        {
            var ord = order(word, i);
        
            if (t[cur].next[ord] === -1)
            {
                t[cur].next[ord] = len(t);
                push(t, new Node(cur, ord));
            }
            
            cur = t[cur].next[ord];
        }
        
        push(t[cur].leaf, index);
    }
    
    solve (text, words)
    {
        var n = len(words), t = this.t;
        var ans = [0] * n, count = [0] * len(t);
        
        var cur = 0;
        for (var i = 0; i < len(text); i += 1)
        {
            cur = this.go(cur, order(text, i));
            
            count[cur] += 1;
        }
        
        for (var cur = 0; cur < len(t); cur += 1)
        {
            if (count[cur])
            {
                for (var node = cur; node !== 0; node = this.exit(node))
                {
                    for (var i = 0; i < len(t[node].leaf); i += 1)
                    {
                        ans[t[node].leaf[i]] += count[cur];
                    }
                }
            }
        }
        
        for (var i = 0; i < n; i += 1)
        {
            println(words[i] + ": \t" + ans[i]);
        }
    }
}

var words = 
[
    "a",
    "aa",
    "abc",
    "abcde",
    "bcd",
    "ee",
    "dba",
    "cea",
    "aedcb"
];
var text = "abcdeeaabcdcdeabcdacb";

var ac = new AhoCorasick();
for (var i = 0; i < len(words); i += 1)
{
    ac.add(words[i], i);
}
print(format(ac.t));
ac.solve(text, words);